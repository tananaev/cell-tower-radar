plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion 33
    defaultConfig {
        applicationId 'com.tananaev.celltowerradar'
        minSdkVersion 19
        targetSdkVersion 33
        versionCode 12
        versionName '2.4'
        multiDexEnabled true
    }
    dexOptions {
        javaMaxHeapSize '4g'
    }
    namespace 'com.tananaev.celltowerradar'

    flavorDimensions 'default'
    productFlavors {
        regular {
            isDefault = true
        }
        google
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'com.google.android.gms:play-services-maps:18.1.0'
    implementation 'com.google.code.gson:gson:2.9.1'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    googleImplementation platform('com.google.firebase:firebase-bom:31.0.1')
    googleImplementation 'com.google.firebase:firebase-analytics-ktx'
    googleImplementation 'com.google.firebase:firebase-crashlytics'
    googleImplementation 'com.google.android.gms:play-services-ads:21.3.0'
    googleImplementation 'com.google.android.play:review-ktx:2.0.1'
}

if (getGradle().getStartParameter().getTaskRequests().toString().contains('Google')) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'

    task copyJson(type: Copy) {
        from '../../environment/firebase'
        into '.'
        include 'cell-tower-radar.json'
        rename('cell-tower-radar.json', 'google-services.json')
    }

    afterEvaluate {
        preBuild.dependsOn copyJson
    }
}
